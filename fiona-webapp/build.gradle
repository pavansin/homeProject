buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
        classpath('com.moowork.gradle:gradle-node-plugin:1.0.1')
    }
}

group = 'avagon'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'
apply plugin: 'war'

node {
    version = '6.9.1'
    npmVersion = '3.10.8'
    download = true
    // distBaseUrl = Custom artifactory location here for node/npm.
}

war {
    baseName = 'fiona-webapp'
    version = '1.0.0-SNAPSHOT'
}

repositories {
    mavenCentral()
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

task clientDependenciesDev(type: NpmTask) {
    args = ['install']
}

build.dependsOn clientDependenciesDev, gulp_default

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = war.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from war
            into stageDir
        }
    }
}